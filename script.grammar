statement: <for> | <if> | <assign> | <expression>;

for : "for" <ident> "in" (<number> /\.\.\.?/)? <number> ("by" <number>)? <block>;

if : "if" <expression> <block>;

assign : <ident> '=' <expression>;

expression: <call> | <not>;

ident: /[a-zA-Z_][a-zA-Z_0-9]*/;

number: /[1-9][0-9]*/ | /0x[0-9a-fA-F]+/ | '0' | "true" | "false";

call: <ident> '(' <expression> (',' <expression>)* ')';

not: '!' <or> | <or>;

or : <and> ( "||" <and> )*;

and : <equality> ( "&&" <equality> )*;

equality : <comparison> ( ("==" | "!=") <comparison> )*;

comparison : <sum> ( ("<=" | ">=" | '<' | '>') <sum> )*;

sum : <product> ( ('+' | '-') <product> )*;

product : <value> ( ('*' | '/' | '%') <value> )*;

value : <number> | <ident> | '(' <expression> ')';

block: '{' <statement>* '}';

program : /^/ <statement>* /$/;
